CLASS:: HilbertWIm
summary:: Applies the Imaginary part of the Hilbert Transform to an input signal. [1]
categories:: Libraries>Hilbert>Pseudo-UGens, UGens>Filters>Nonlinear
related:: Classes/HilbertW, Classes/HilbertWRe

DESCRIPTION::

Offers the Hilbert Transform of an input signal via Weaver's emphasis::Third Method::. footnote::Weaver, Donald. “A Third Method of Generation and Detection of Single-Sideband Signals.” Proceedings of the IRE, vol. 44, no. 12, 1956, pp. 1703–1705.::

The Hilbert Transform, returning the second of two signals in emphasis::phase-quadrature::. Considered as a complex emphasis::analytic signal::, footnote::Smith, J.O. “Analytic Signals and Hilbert Transform Filters”, in Mathematics of the Discrete Fourier Transform (DFT) with Audio Applications, Second Edition, https://ccrma.stanford.edu/~jos/st/Analytic_Signals_Hilbert_Transform.html, online book, 2007 edition, accessed 2017-08-08.:: the first may be regarded as the emphasis::real:: component and the second as the emphasis::imaginary::.

[1] this is a composite pseudo UGen. link::Classes/HilbertWIm:: is built with link::Classes/Delay1::, link::Classes/Delay2::, link::Classes/Impulse::, link::Classes/LocalBuf::, link::Classes/FFT::, link::Classes/IFFT::, and link::Classes/PV_BrickWall::.

CLASSMETHODS::

METHOD:: ar

ARGUMENT:: in

The input signal to transform.

ARGUMENT:: size

The size of the FFT used for emphasis::Weaver Half-Band:: filtering.

ARGUMENT:: mul

Output will be multiplied by this value.

ARGUMENT:: add

This value will be added to the output.

returns:: The imaginary part of the Hilbert Transform.


SECTION:: Frequency response

Please review the discussion link::Classes/HilbertW#Frequency%20response#found here::.


EXAMPLES::

code::
/*

FFT analysis functions

NOTE: FFT returns linear frequency

*/
(
~calcMag = { arg kernel;

    var fftResponse, fftMag;

    // FFT analysis here!
    fftResponse = fft(
        kernel.as(Signal),
        Signal.newClear(kernel.size),
        Signal.fftCosTable(kernel.size)
    );

    // find (& trim magnitude)
    fftMag = fftResponse.magnitude;
    fftMag = fftMag.copyFromStart((kernel.size/2).asInteger);

    fftMag;
};
)


/*
        TEST Weaver : HilbertWIm

        Impulse Response
*/
(
var size, dur, freq;
var overSample, start;
var plotDbMin, plotDbMax;

// kernel size
size = 2048;

overSample = 2;

// plot params
plotDbMin = -60.0;
plotDbMax = 6.0;

start = size  - ((size/2).asInt) - s.options.blockSize;  // offset - for kernel normalized to sample delay

dur = size / s.sampleRate;  // in seconds
freq = dur.reciprocal;

fork {
	{ // imaginary
		HilbertWIm.ar(
			Impulse.ar(freq), // imulse test, one pulse over fftbuffer size
		size: size);
	}.loadToFloatArray(overSample * dur, s, {
		|arr|
		defer {
			arr = arr.copyRange(start, start+size-1);
			arr.plot("Imaginary; HilbertWIm; Impulse input", minval: -1, maxval: 1);
		}
		}
	);

	0.5.wait;

	{ // plot magnitude - imaginary
		HilbertWIm.ar( // imaginary
			Impulse.ar(freq), // imulse test, one pulse over fftbuffer size
		size: size);
	}.loadToFloatArray(overSample * dur, s, {
		|arr|
		defer {
			arr = arr.copyRange(start, start+size-1);
			~calcMag.value(arr).ampdb.plot(
				name: "Imaginary Magnitude Response",
				minval: plotDbMin,
				maxval: plotDbMax
			);
		}
		}
	);

}
)

/*
        TEST Weaver : HilbertWIm

        single cosine cycle: period = size
*/

(
var size, dur, freq;
var overSample, start;

size = 2048;
overSample = 3;
// plot: jump to the 2nd cycle, to account for "warm up" of Im kernel
start = 2 * size - s.options.blockSize;  // offset - to sync cycle

dur = size / s.sampleRate;  // in seconds
freq = dur.reciprocal;

{ // imaginary
	HilbertWIm.ar(
		SinOsc.ar(freq, pi/2), // cosine test, one cycle over fftbuffer size
		size: size);
}.loadToFloatArray(overSample * dur, s, {
	|arr|
	defer {
		arr = arr.copyRange(start, start+size-1);
		arr.plot("Imaginary; HilbertWIm; Cosine input", minval: -1, maxval: 1);
	}
}
);
)
::
