CLASS:: HilbertHRe
summary:: Applies the Real part of the Hilbert Transform to an input signal. [1]
categories:: Libraries>Hilbert>Pseudo-UGens, UGens>Filters>Nonlinear
related:: Classes/HilbertH, Classes/HilbertHIm

DESCRIPTION::

Offers the Hilbert transform of an input signal via Weaver's emphasis::Second Method::,footnote::Weaver, Donald. “A Third Method of Generation and Detection of Single-Sideband Signals.” Proceedings of the IRE, vol. 44, no. 12, 1956, pp. 1703–1705.:: known as emphasis::Hartley Phasing::.footnote::US Patent 1,666,206, Modulation System, April 17, 1928, United States Patent and Trademark Office.::

The Hilbert Transform, returning the first of two signals in emphasis::phase-quadrature::. Considered as a complex emphasis::analytic signal::, footnote::Smith, J.O. “Analytic Signals and Hilbert Transform Filters”, in Mathematics of the Discrete Fourier Transform (DFT) with Audio Applications, Second Edition, https://ccrma.stanford.edu/~jos/st/Analytic_Signals_Hilbert_Transform.html, online book, 2007 edition, accessed 2017-08-08.:: the first may be regarded as the emphasis::real:: component and the second as the emphasis::imaginary::.

[1] this is a composite pseudo UGen. link::Classes/HilbertHRe:: is built with link::Classes/DelayN::. Method link::#*arConv:: also includes link::Classes/Convolution2:: and link::Classes/LocalBuf::.

CLASSMETHODS::

METHOD:: ar

ARGUMENT:: in

The input signal to transform.

ARGUMENT:: size

The size of the kernel used for emphasis::Hartley Phasing:: filtering.

ARGUMENT:: mul

Output will be multiplied by this value.

ARGUMENT:: add

This value will be added to the output.

returns:: The real part of the Hilbert Transform.

METHOD:: arConv

Return the real transform via convolution.

ARGUMENT:: in

The input signal to transform.

ARGUMENT:: size

The size of the kernel used for emphasis::Hartley Phasing:: filtering.

ARGUMENT:: mul

Output will be multiplied by this value.

ARGUMENT:: add

This value will be added to the output.

returns:: The real part of the Hilbert Transform.

METHOD:: calcCoeffs

Generate real coefficients.

ARGUMENT:: size

The size of the kernel used for emphasis::Hartley Phasing:: filtering.

DISCUSSION::

WARNING::To be deprecated! Use link::Classes/Signal#*hilbert#Signal *hilbert.real:: instead.::

code::
Signal.hilbert(1000).real.plot("Hilbert Real Coefficients")
::


SECTION:: Frequency response

Please review the discussion link::Classes/HilbertH#Frequency%20response#found here::.


EXAMPLES::

SUBSECTION:: ar

code::
/*

FFT analysis functions

NOTE: FFT returns linear frequency

*/
(
~calcMag = { arg kernel;

    var fftResponse, fftMag;

    // FFT analysis here!
    fftResponse = fft(
        kernel.as(Signal),
        Signal.newClear(kernel.size),
        Signal.fftCosTable(kernel.size)
    );

    // find (& trim magnitude)
    fftMag = fftResponse.magnitude;
    fftMag = fftMag.copyFromStart((kernel.size/2).asInteger);

    fftMag;
};
)


/*
        TEST Hartley : HilbertHRe

        Impulse Response
*/
(
var size, dur, freq;
var overSample, start;
var plotDbMin, plotDbMax;

// kernel size
size = 2048;

overSample = 2;

// plot params
plotDbMin = -60.0;
plotDbMax = 6.0;

start = size - s.options.blockSize;  // offset - for kernel normalized to sample delay

dur = size / s.sampleRate;  // in seconds
freq = dur.reciprocal;

fork {
	{ // real
		HilbertHRe.ar(
			Impulse.ar(freq), // imulse test, one pulse over fftbuffer size
		size: size);
	}.loadToFloatArray(overSample * dur, s, {
		|arr|
		defer {
			arr = arr.copyRange(start, start+size-1);
			arr.plot("Real; HilbertHRe; Impulse input", minval: -1, maxval: 1);
		}
		}
	);

	0.5.wait;

	{ // plot magnitude - real
		HilbertHRe.ar( // real
			Impulse.ar(freq), // imulse test, one pulse over fftbuffer size
		size: size);
	}.loadToFloatArray(overSample * dur, s, {
		|arr|
		defer {
			arr = arr.copyRange(start, start+size-1);
			~calcMag.value(arr).ampdb.plot(
				name: "Real Magnitude Response",
				minval: plotDbMin,
				maxval: plotDbMax
			);
		}
		}
	);

}
)

/*
        TEST Hartley : HilbertHRe

        single cosine cycle: period = size
*/

(
var size, dur, freq;
var overSample, start;

size = 2048;
overSample = 4;
// plot: jump to the 2nd cycle, to account for "warm up" of Im kernel
start = 2 * size + ((size/ 2).floor).asInt - s.options.blockSize;  // offset - to sync cycle, for kernel normalized to sample delay

dur = size / s.sampleRate;  // in seconds
freq = dur.reciprocal;

{ // real
	HilbertHRe.ar(
		SinOsc.ar(freq, pi/2), // cosine test, one cycle over fftbuffer size
		size: size);
}.loadToFloatArray(overSample * dur, s, {
	|arr|
	defer {
		arr = arr.copyRange(start, start+size-1);
		arr.plot("Real; HilbertHRe; Cosine input", minval: -1, maxval: 1);
	}
}
);
)
::

SUBSECTION:: arConv

code::
/*

FFT analysis functions

NOTE: FFT returns linear frequency

*/
(
~calcMag = { arg kernel;

    var fftResponse, fftMag;

    // FFT analysis here!
    fftResponse = fft(
        kernel.as(Signal),
        Signal.newClear(kernel.size),
        Signal.fftCosTable(kernel.size)
    );

    // find (& trim magnitude)
    fftMag = fftResponse.magnitude;
    fftMag = fftMag.copyFromStart((kernel.size/2).asInteger);

    fftMag;
};
)


/*
        TEST Hartley : HilbertHRe

        Impulse Response
*/
(
var size, dur, freq;
var overSample, start;
var plotDbMin, plotDbMax;

// kernel size
size = 2048;

overSample = 2;

// plot params
plotDbMin = -60.0;
plotDbMax = 6.0;

start = size - s.options.blockSize;  // offset - for kernel normalized to sample delay

dur = size / s.sampleRate;  // in seconds
freq = dur.reciprocal;

fork {
	{ // real
		HilbertHRe.arConv(
			Impulse.ar(freq), // imulse test, one pulse over fftbuffer size
		size: size);
	}.loadToFloatArray(overSample * dur, s, {
		|arr|
		defer {
			arr = arr.copyRange(start, start+size-1);
			arr.plot("Real; HilbertHRe; Impulse input", minval: -1, maxval: 1);
		}
		}
	);

	0.5.wait;

	{ // plot magnitude - real
		HilbertHRe.arConv( // real
			Impulse.ar(freq), // imulse test, one pulse over fftbuffer size
		size: size);
	}.loadToFloatArray(overSample * dur, s, {
		|arr|
		defer {
			arr = arr.copyRange(start, start+size-1);
			~calcMag.value(arr).ampdb.plot(
				name: "Real Magnitude Response",
				minval: plotDbMin,
				maxval: plotDbMax
			);
		}
		}
	);

}
)

/*
        TEST Hartley : HilbertHRe

        single cosine cycle: period = size
*/

(
var size, dur, freq;
var overSample, start;

size = 2048;
overSample = 4;
// plot: jump to the 2nd cycle, to account for "warm up" of Im kernel
start = 2 * size + ((size/ 2).floor).asInt - s.options.blockSize;  // offset - to sync cycle, for kernel normalized to sample delay

dur = size / s.sampleRate;  // in seconds
freq = dur.reciprocal;

{ // real
	HilbertHRe.arConv(
		SinOsc.ar(freq, pi/2), // cosine test, one cycle over fftbuffer size
		size: size);
}.loadToFloatArray(overSample * dur, s, {
	|arr|
	defer {
		arr = arr.copyRange(start, start+size-1);
		arr.plot("Real; HilbertHRe; Cosine input", minval: -1, maxval: 1);
	}
}
);
)
::
